import os
import fmt
import path
import syscall
import parker.log as * 
import parker.util
import compress.tgz
import fs

var version = '0.1.0'

fn main():void! {
    if util.arg_version() {
        fmt.printf('parker version: %v\n', version)
        return
    }

    if util.arg_verbose() {
        tgz.verbose = true
        set_verbose()
    }

    var args = os.args()

    // - è¯»å–å½“å‰ç¨‹åºæ‰€åœ¨ç›®å½•ï¼Œæ‰¾åˆ° runner ç¨‹åº
    if args.len() < 2 {
        throw errorf('args failed')
    }

    // å¯èƒ½æ˜¯ä¸€ä¸ªç»å¯¹è·¯å¾„ä¹Ÿå¯ä»¥æ˜¯ä¸€ä¸ªç›¸å¯¹ path
    var exec_path = os.exe()
    logf('exec_path: %v', exec_path)

    var target_name = args[1]

    // - è¯»å–å½“å‰å·¥ä½œç›®å½•, ç¡®å®šéœ€è¦æ‰“åŒ…çš„ç›®å½•
    var workdir = syscall.getcwd()
    logf('workdir: %v, target: %v', workdir, target_name)
    var target_path = path.join(workdir, target_name)

    // - æ£€æŸ¥æ–‡ä»¶æ˜¯å¦å­˜åœ¨
    if !path.exists(target_path) {
        throw errorf('file=%v notfound', target_path)
    }

    logf('target_path: %v found', target_path)

    workdir = path.dir(target_path)
    target_name = path.base(target_path)

    logf('new split workdir: %v, target: %v', workdir, target_name)

    // .target_name å°† target_name å†™å…¥åˆ°æ–‡ä»¶ .target_name ä¸­éšå‹ç¼©åŒ…ä¸€èµ·å‹ç¼©
    var target_name_f = fs.open('.target_name', syscall.O_RDWR | syscall.O_CREAT, 0755)
    target_name_f.write(target_name as [u8])
    target_name_f.close()

    // - å‹ç¼©åˆ°å·¥ä½œç›®å½•å³å¯, åå­—å°±å« {target_name}.tar.gz
    var tgz_name = fmt.sprintf('%v.tar.gz', target_name)

    // new target_name + 'd' fd, node -> noded
    var output_name = fmt.sprintf('%v-c', target_name)

    // - éå†å½“å‰å·¥ä½œç›®å½•ä¸­çš„æ‰€æœ‰æ–‡ä»¶ï¼Œè¿›è¡Œå‹ç¼©
    var sources = os.listdir(workdir)
    [string] temp = []
    for s in sources {
        if {tgz_name, output_name}.contains(s) {
            logf('tgz encode source %v will continue', s)
            continue
        }

        temp.push(s)
    }
    sources = temp

    tgz.encode(workdir, tgz_name, sources)

    logf('encode to tgz_name %v in %v', tgz_name, workdir)

    // å¦‚æœå­˜åœ¨ç¯å¢ƒå˜é‡ RUNNER_PATH, åˆ™æœ‰ä¼˜å…ˆè¯»å–ç¯å¢ƒå˜é‡
    var runner_path =  syscall.get_env('RUNNER_PATH')
    if runner_path == '' {
        var parker_dir = path.dir(exec_path)
        runner_path = path.join(parker_dir, 'runner')
    }

    if !path.exists(runner_path) {
        throw errorf('runner file=%v notfound', runner_path)
    }

    logf('runner_path=%v found', runner_path)

    // open and create
    var output_fd = fs.open(output_name, syscall.O_CREAT|syscall.O_RDWR|syscall.O_TRUNC, 0755)

    // - append runner
    var runner_fd = fs.open(runner_path, syscall.O_RDONLY, 0666)
    var runner_content = runner_fd.content()
    runner_fd.close()

    logf('read runner_fd len: %v', runner_content.len())
    var len = output_fd.write(runner_content as [u8])
    logf('write runner to output success, write len=%v', len)

    // - append tgz
    var tgz_fd = fs.open(tgz_name, syscall.O_RDONLY, 0666)
    var tgz_content = tgz_fd.content()
    tgz_fd.close()

    logf('read tgz_fd len: %v', tgz_content.len())
    len = output_fd.write(tgz_content as [u8])
    logf('write tgz to output success, write len=%v', len)

    os.remove(tgz_name)
    os.remove('.target_name')

    var tgz_size = tgz_content.len()
    logf('stat tgz %v get size=%d', tgz_name, tgz_size)
    var tgz_size_str = fmt.sprintf('%016d', tgz_size)
    output_fd.write(tgz_size_str as [u8])
    logf('tag_size to output success, size_str=%v', tgz_size_str)

    logf('runner %s make successful', path.join(workdir, output_name))

    // - ä¿¡æ¯è¾“å‡º
    fmt.printf('%s\n', output_name)
    for i, item in sources {
        if item == '.target_name' {
            continue
        }

        if i < sources.len() - 1 {
            fmt.printf('â”œâ”€â”€ %s\n', item)
        } else {
            fmt.printf('â””â”€â”€ %s\n', item)
        }
    }
    fmt.printf('ğŸ» parker successful\n')
}